plugins {
    id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()    
    compile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.2"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

jar {
    // include license into jar
    from 'LICENSE.txt'
}

// package source into a jar file
task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// define artifacts for upload
artifacts {
    archives jar
    archives packageSources
}

bintray {
    // to publish run gradle with task bintrayUpload
    user = this.hasProperty('bintrayUser') ? bintrayUser : ''
    key = this.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
    configurations = ['archives']
    dryRun = false
    publish = !project.version.endsWith('-SNAPSHOT')
    pkg {
        def githubUrl = 'https://github.com/leontebbens/chromedriver-updater-plugin'
        repo = 'maven'
        name = 'eu.leontebbens.gradle.chromedriver-updater-plugin'
        desc = 'Checks and downloads latest chromedriver for selenium'
        websiteUrl = githubUrl
        issueTrackerUrl = "$githubUrl/issues"
        vcsUrl = "${githubUrl}.git"
        licenses = ['Apache-2.0']
        labels = ['gradle', 'selenium', 'plugin', 'chromedriver']
        publicDownloadNumbers = true
        version {
            name = project.version
            attributes = ['gradle-plugin': "eu.leontebbens.gradle:${project.group}:chromedriver-updater-plugin"]
        }
    }
}